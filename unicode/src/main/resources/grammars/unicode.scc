Package de.prob.unicode;
Helpers
    /* some duplicates in whitespace and non_identifier_characters, no need to remove them */
    separator = [[[[[[[',' + '<'] + ['&' + ['>' + '=']]]+[['|' + '{'] + ['}' + '(']]]+[[')' + '\'] + ['.' + ':']]] + [[[['\' + 0x2208] + ['/' +  0x2288]] + [[0x2284 + '<'] + [0x2286 + 0x2216]]] + [[['.' + 0x2025] + [0x2115 + 0x2205]] +[[0x2223 + 0x22a5] + ['!' + 0x2200]]]]] +[[[[[0x2229+ 0x25c1]+[0x25b7+ 0x2a64]]+ [[0x2a65 + '%'] + [0x03bb+ 0x2982]]] + [[[0x22c2+ 0x003b]+ [0x2218+ 0xe102]]+ [[0x2297+ 0x2225]+ [0x2254+ 0x2124]]]]+ [[[[0x2227+ '=' ]+ [0x21d2+ 0x21d4]]+ [[0x00ac+ 0x00b7]+ ['~' +  0x223c]]]+ [[[0xe100+ 0xe101]+ [0x21f8+ '-']] +  [[0x2192+ 0x2914]+ [0x21a3+ 0x2900]]]]]]+ [[[[[0x21a0+ 0x2916]+ [0x005e+ 0x2228]]+ [[0x2119+ 0x2223]+ [0x2260+ 0x2194]]]+ [[[0xe103+ 0x2264]+ [0x2265+ 0x00f7]]+ [['*' + 0x2217]+ ['-' + 0x2212]]]]+ [[[['#' + 0x2203] + ['+' + 0x21a6]]+ [[0x22a4 + '>'] + [0x2282 +  0x222a]]]+[[0x2209+ '*'] + [0x00d7+ 0x22c3]]]]];
    space = [[' ' + 9] + [10 + 13]];
	double_quote = '"';
	letter = [['a' .. 'z'] + ['A' .. 'Z']];
	digit = ['0' .. '9'];
Tokens
	string = double_quote ([[0x0000 .. 0xffff] - [double_quote + '\']] | '\' [0x0000 .. 0xffff])* double_quote;
    typeof_open = '/*';
    typeof_close = '*/';
    truncated_set_size = ('#' | '\#') digit+;
	in = ':' | '\in' | 0x2208;
	notsubseteq = '/<:' | '\notsubseteq' | 0x2288;
	notsubset = '/<<:'| '\notsubset' | 0x2284;
	subseteq = '<:' | '\subseteq' | 0x2286;
	setminus = '\' | '\setminus' | 0x2216;
	dotdot = '..' | '\upto' | 0x2025;
	dotdotdot = '...' | '\ldots' | 0x2026; 
	nat1 = 'NAT1' | '\nat1'| 0x2115 0x0031;
	nat = 'NAT' | '\nat'| 0x2115;
	emptyset = '{}' | '\emptyset'| 0x2205;
	bcmsuch = ':|' | '\bcmsuch'| ':'0x2223;
	bfalse = 'false' | '\bfalse'| 0x22a5;
	forall = '!' | '\forall'| 0x2200;
	exists = '#' | '\exists'| 0x2203;
    mapsto = '|->' | '\mapsto'| 0x21a6;
	btrue = 'true' | '\btrue'| 0x22a4;
	subset = '<<:' | '\subset'| 0x2282;
	bunion = '\/'| '\bunion'| 0x222a;
	binter = '/\'| '\binter'| 0x2229;
	domres = '<|' | '\domres'| 0x25c1;
	ranres = '|>' | '\ranres'| 0x25b7;
	domsub = '<<|' | '\domsub'| 0x2a64;
	ransub = '|>>' | '\ransub'| 0x2a65;
	lambda = '%' | '\lambda'| 0x03bb;
	oftype = 'oftype' |'\oftype'| 0x2982;
	notin = '/:' |  '\notin'| 0x2209;
	cprod = '**' | '\cprod' | 0x00d7;
	union = 'UNION' | '\Union'| 0x22c3;
	inter = 'INTER' | '\Inter'| 0x22c2;
	fcomp = '\fcomp'| 0x003b;
	bcomp = 'circ' | '\bcomp'| 0x2218;
	strel = '<<->>' | '\strel'| 0xe102;
	dprod = '><' | '\dprod'| 0x2297;
	pprod = '||' | '\pprod'| 0x2225;
	bcmeq = ':=' | '\bcmeq'| 0x2254;
	bcmin = '::' | '\bcmin'| ':'0x2208;
	intg = 'INT' | '\intg'| 0x2124;
	land = '&' | '\land'| 0x2227;
	limp = '=>' | '\limp'| 0x21d2;
	leqv = '<=>' | '\leqv'| 0x21d4;
	lnot = 'not' | '\lnot'| 0x00ac;
	qdot = '.' | '\qdot'| 0x00b7;
	conv = '~' | '\conv'| 0x223c;
	trel = '<<->' | '\trel'| 0xe100;
	srel = '<->>' | '\srel'| 0xe101;
	pfun = '+->' | '\pfun'| 0x21f8;
	tfun = '-->' | '\tfun'| 0x2192;
	pinj = '>+>' | '\pinj'| 0x2914;
	tinj = '>->' |'\tinj'| 0x21a3;
	psur = '+>>' | '\psur'| 0x2900;
	tsur = '->>' | '\tsur'| 0x21a0;
	tbij = '>->>' | '\tbij'| 0x2916;
	expn = '\expn'| 0x005e;
	lor = 'or' | '\lor'| 0x2228;
	pow1 = 'POW1' | '\pow1'| 0x2119 0x0031;
	pow = 'POW' | '\pow'| 0x2119;
	mid = '\mid' | '|' | 0x2223;
	neq = '/=' | '\neq'| 0x2260;
	rel = '<->' |'\rel'| 0x2194;
	ovl = '<+' | '\ovl'| 0xe103;
	leq = '<=' | '\leq'| 0x2264;
	geq = '>=' | '\geq'| 0x2265;
	div = '/' | '\div'| 0x00f7;
	mult = '*' | 0x2217;
	minus = '-' | 0x2212;
	lbrace = '{' | '\{';
	rbrace = '}' | '\}';

	take = '/|\';
	drop = '\|/';

	identifier_literal = letter (letter | digit | '_')*;

	any_char = ([[0x0000 .. 0xffff] - [[separator + space ] + double_quote]])*;
	whitespace = space*;
	separator = separator;
